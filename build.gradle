plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.5.20' apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.5.20' apply false
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        google()
    }
}

// Use system environment variables or default to empty values
ext["ossrhUsername"] = System.getenv('OSSRH_USERNAME') ?: ''
ext["ossrhPassword"] = System.getenv('OSSRH_PASSWORD') ?: ''
ext["sonatypeStagingProfileId"] = System.getenv('SONATYPE_STAGING_PROFILE_ID') ?: ''

ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID') ?: ''
ext["signing.key"] = System.getenv('SIGNING_KEY') ?: ''
ext["signing.password"] = System.getenv('SIGNING_PASSWORD') ?: ''

// Read local.properties file first if it exists
Properties props = new Properties()
File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    new FileInputStream(secretPropsFile).withCloseable { stream -> props.load(stream) }
    props.each { name, value -> ext[name] = value }
}

nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = sonatypeStagingProfileId
            username = ossrhUsername
            password = ossrhPassword

            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

subprojects {
    if (name == 'sample') return

    // Publishing configuration
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    group = publishGroupId
    version = publishVersion

    publishing {
        publications.withType(MavenPublication) {
            pom {
                description = "Idiomatic gRPC for Kotlin Mutliplatform"
                url = "https://github.com/darvld/krpc"

                licenses {
                    license {
                        name = "Apache License 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0"
                    }
                }

                developers {
                    developer {
                        id = 'darvld'
                        name = 'Dario Valdespino'
                        email = 'dvaldespino00@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:github.com/darvld/krpc.git'
                    developerConnection = 'scm:git:ssh://github.com/darvld/krpc.git'
                    url = 'https://github.com/darvld/krpc/tree/main'
                }
            }
        }
    }

    signing {
        useInMemoryPgpKeys(
                rootProject.ext["signing.keyId"],
                rootProject.ext["signing.key"],
                rootProject.ext["signing.password"]
        )
        sign publishing.publications
    }
}