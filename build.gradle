plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.5.20' apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.5.20' apply false
}

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        google()
    }
}

subprojects {
    if (name == 'samples') return

    // Publishing configuration
    apply plugin: 'maven-publish'

    group = publishGroupId
    version = publishVersion

    publishing {
        publications.withType(MavenPublication) {
            pom {
                description = "Idiomatic gRPC for Kotlin Mutliplatform"
                url = "https://github.com/darvld/krpc"

                licenses {
                    license {
                        name = "Apache License 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0"
                    }
                }

                developers {
                    developer {
                        id = 'darvld'
                        name = 'Dario Valdespino'
                        email = 'dvaldespino00@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:github.com/darvld/krpc.git'
                    developerConnection = 'scm:git:ssh://github.com/darvld/krpc.git'
                    url = 'https://github.com/darvld/krpc/tree/main'
                }
            }
        }
    }
}